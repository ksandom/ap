# Calculate a position from an angle and a distance. --calculateNewPosition=Category,value,angle,distance,fromLongitude,fromLatitude ~ route

parameters {"Category":"Tmp", "value":"newPosition", "angleInDegrees":"90", "distanceInKM":"10" ,"fromLongitude":"", "fromLatitude":""}

pass isolate
    getCategory Local
    setNested
    outNow

# Calculate the offset for the waypoint.
degreesToRadians Local,angle,~!Local,angleInDegrees!~
KMToDegrees Local,distanceInDegrees,~!Local,distanceInKM!~
angleAndDistanceToCoord Route,offsets,~!Local,angle!~,~!Local,distanceInDegrees!~


# Apply the offset.
basicMaths Route,newPosition-longitude,~!Local,fromLongitude!~,+,~!Route,offsets,x!~
basicMaths Route,newPosition-latitude,~!Local,fromLatitude!~,+,~!Route,offsets,y!~

# Add the coordinates.
setNested ~!Local,Category!~,~!Local,value!~,longitude,~!Route,newPosition-longitude!~
setNested ~!Local,Category!~,~!Local,value!~,latitude,~!Route,newPosition-latitude!~

debug 1,newPositon: ~!Local,Category!~,~!Local,value!~: angle=~!Local,angle!~ (~!Local,angleInDegrees!~) distance=~!Local,distanceInDegrees!~ (~!Local,distanceInKM!~) from=~!Local,fromLongitude!~,~!Local,fromLatitude!~ offsets=~!Route,offsets!~ newPosition=~!~!Local,Category!~,~!Local,value!~!~

# Clean up
unset Route,offsets
unset Route,newPosition-longitude
unset Route,newPosition-latitude

makeAvailable ~!Local,Category!~,~!Local,value!~
