# Enter into a line in a controlled way. This is useful to enter into a precise approach. --enterLine=instanceName=default,radius,angleInDegrees,lineEntryLongitude,lineEntryLatitude,extendKM=0 "entry" is the line entry. "current" is the aircraft position. ~ route,approach,enter,line
parameters {"instanceName":"default","radius":"1","angleInDegrees":"0","lineEntryLongitude":"0","lineEntryLatitude":"0","currentLongitude":"0","currentLatitude":"0","extendKM":"1"}

isolate
	getCategory Local
	nested
	outNow

keepPoint ~!Local,instanceName!~,interestPoints,~!Local,currentLongitude!~,~!Local,currentLatitude!~,500,originAtTimeOfCalculation
keepPoint ~!Local,instanceName!~,interestPoints,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~,500,lineEntry

apToMeWithContext ~!Local,instanceName!~,routePlanning,inverseRunwayAngle
radiansToDegrees Local,inverseRunwayAngleDegrees,~!Me,inverseRunwayAngle!~

apToMeWithContext ~!Local,instanceName!~,routePlanning,runwayAngle
radiansToDegrees Local,runwayAngleDegrees,~!Me,runwayAngle!~

# TODO Take into account that when we enter into this, we might not be facing the right way. We will therefore need to turn, and this will have an impact on entering into the arc .

# We may want to put an extra way point a short time before entry into the line to get the plane going on a straight couse in a stable way. We'll need to calculate it here to offset everything, but add it last, so it's in the right order.
pass
	if ~!Local,extendKM!~,!=,0,
		debug 1,No extention before entering approach.
		calculateNewPosition Tmp,originPosition,~!Local,inverseRunwayAngleDegrees!~,~!Local,radius!~,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~
	else
		debug 1,~!Local,extendKM!~KM extention before entering approach.
		setNested Tmp,originPosition,longitude,~!Local,lineEntryLongitude!~
		setNested Tmp,originPosition,latitude,~!Local,lineEntryLatitude!~


# TODO This appears to be incorrect.
# Calculate pivot point.
rotateAngleInDegrees Local,pivotAngle,~!Local,inverseRunwayAngleDegrees!~,270
calculateNewPosition Local,pivot,~!Local,pivotAngle!~,~!Local,radius!~,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~


debug 1,pivot calculations: runwayAngle=~!Local,runwayAngleDegrees!~,~!Local,inverseRunwayAngleDegrees!~   pivotAngle=~!Local,pivotAngle!~ r=~!Local,radius!~ orgPos=~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~    pivot=~!Local,pivot!~

keepPoint ~!Local,instanceName!~,interestPoints,~!Local,pivot,longitude!~,~!Local,pivot,latitude!~,500,pivot-~!Local,instanceName!~

# Calculate arc entry point. (currentPosition, pivotPosition, radius)
calculateArcEntryPoint Local,arcEntryPoint,~!Local,radius!~,~!Local,currentLongitude!~,~!Local,currentLatitude!~,~!Local,pivot,longitude!~,~!Local,pivot,latitude!~
debug 0,pivot=~!Local,pivot,longitude!~,~!Local,pivot,latitude!~ r=~!Local,radius!~

# Add the entry waypoint and trigger the arc turn.
addWaypoint ~!Local,instanceName!~,~!Local,arcEntryPoint,longitude!~,~!Local,arcEntryPoint,latitude!~,Arc,entry,~!Local,instanceName!~,~!Local,pivot,longitude!~,~!Local,pivot,latitude!~
keepPoint ~!Local,instanceName!~,interestPoints,~!Local,arcEntryPoint,longitude!~,~!Local,arcEntryPoint,latitude!~,500,Arc-entry-~!Local,instanceName!~

# Add extended waypoint.
if ~!Local,extendKM!~,!=,0,
	# addWaypoint ~!Local,instanceName!~,~!Tmp,originPosition,longitude!~,~!Tmp,originPosition,latitude!~,Arc,exit,~!Local,instanceName!~
	# keepPoint ~!Local,instanceName!~,interestPoints,~!Tmp,originPosition,longitude!~,~!Tmp,originPosition,latitude!~,500,Arc-exit-~!Local,instanceName!~
	keepPoint ~!Local,instanceName!~,interestPoints,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~,500,lineEntry
else
	addWaypoint ~!Local,instanceName!~,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~,Arc,exit,~!Local,instanceName!~
	keepPoint ~!Local,instanceName!~,interestPoints,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~,500,Arc-exit-~!Local,instanceName!~

# Cleanup
unset Me,inverseRunwayAngle
