# Enter into a line in a controlled way. This is useful to enter into a precise approach. --enterLine=instanceName=default,radius,angleInDegrees,lineEntryLongitude,lineEntryLatitude,extendKM=0 "entry" is the line entry. "current" is the aircraft position. ~ route,approach,enter,line
parameters {"instanceName":"default","radius":"1","angleInDegrees":"0","lineEntryLongitude":"0","lineEntryLatitude":"0","currentLongitude":"0","currentLatitude":"0","extendKM":"1"}

# Calculate reverseAngle.
rotateAngleInDegrees Tmp,reverseAngleInDegrees,~!Local,angleInDegrees!~,180

# We may want to put an extra way point a short time before entry into the line to get the plane going on a straight couse in a stable way. We'll need to calculate it here to offset everything, but add it last, so it's in the right order.
if ~!Local,extendKM!~,!=,0,
	calculateNewPosition Tmp,originPosition,~!Tmp,reverseAngleInDegrees!~,~!Local,radius!~,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~
else
	setNested Tmp,originPosition,longitude,~!Local,lineEntryLongitude!~
	setNested Tmp,originPosition,latitude,~!Local,lineEntryLatitude!~

# Calculate pivot point.
rotateAngleInDegrees Local,pivotAngle,~!Tmp,reverseAngleInDegrees!~,270
calculateNewPosition Local,pivot,~!Local,pivotAngle!~,~!Local,radius!~,~!Tmp,originPosition,longitude!~,~!Tmp,originPosition,latitude!~

# Calculate arc entry point. (currentPosition, pivotPosition, radius)
calculateArcEntryPoint Local,arcEntryPoint,~!Local,radius!~,~!Local,currentLongitude!~,~!Local,currentLatitude!~,~!Local,pivot,longitude!~,~!Local,pivot,latitude!~

# Add the entry waypoint and trigger the arc turn.
addWaypoint ~!Local,instanceName!~,~!Local,arcEntryPoint,longitude!~,~!Local,arcEntryPoint,latitude!~,Arc,entry,~!Local,instanceName!~

# TODO Test: Calculate live radius.
# TODO Create state for an arc turn.

# Add extended waypoint.
if ~!Local,extendKM!~,!=,0,
	addWaypoint ~!Local,instanceName!~,~!Tmp,originPosition,longitude!~,~!Tmp,originPosition,latitude!~,Arc,exit,~!Local,instanceName!~
else
	addWaypoint ~!Local,instanceName!~,~!Local,lineEntryLongitude!~,~!Local,lineEntryLatitude!~,Arc,exit,~!Local,instanceName!~

# Cleanup
unset Tmp,reverseAngleInDegrees
