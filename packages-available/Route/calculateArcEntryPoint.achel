# Calculate arc entry point. (currentPosition, pivotPosition, radius) ~ route,hidden
parameters Category,value,radius,currentLongitude,currentLatitude,pivotLongitude,pivotLatitude

# Figure out the hypotenuse of the triangle of us, pivot, and intended arc meeting point.
2CoordsToDistance Local,approachHypotenuse,~!Local,currentLongitude!~,~!Local,currentLatitude!~,~!Local,pivotLongitude!~,~!Local,pivotLatitude!~
degreesToKM Local,approachHypotenuse,~!Local,approachHypotenuse!~

# Calculate the angle between the vector to the pivot, and the vector to meet with the arc.
rightTriangleGetAngleFromOppositeAndHypotenuse Local,arcEntryRawAngle,~!Local,radius!~,~!Local,approachHypotenuse!~
debug 0,rightTriangleGetAngleFromOppositeAndHypotenuse =,~!Local,arcEntryRawAngle!~,~!Local,radius!~,~!Local,approachHypotenuse!~

debug 0,radiansToDegrees ~!Local,arcEntryRawAngleInDegrees!~,~!Local,arcEntryRawAngle!~
radiansToDegrees Local,arcEntryRawAngleInDegrees,~!Local,arcEntryRawAngle!~

# Calculate our angle from north to the pivot.
2CoordsToAngle Local,approachPivotAngle,~!Local,currentLongitude!~,~!Local,currentLatitude!~,~!Local,pivotLongitude!~,~!Local,pivotLatitude!~
radiansToDegrees Local,approachPivotAngleInDegrees,~!Local,approachPivotAngle!~

# Calculate our angle from north to meet with the arc.
basicMaths Local,approachAngle,~!Local,approachPivotAngleInDegrees!~,-,~!Local,arcEntryRawAngleInDegrees!~

# Calcuate how far away the arc entry point is from the current position.
abcSquared Local,approachDistance,~!Local,approachHypotenuse!~,~!Local,radius!~

# Calculate final position.
calculateNewPosition ~!Local,Category!~,~!Local,value!~,~!Local,approachAngle!~,~!Local,approachDistance!~,~!Local,currentLongitude!~,~!Local,currentLatitude!~
debug 0,calculateNewPosition ~!Local,Category!~,~!Local,value!~,~!Local,approachAngle!~,~!Local,approachDistance!~,~!Local,currentLongitude!~,~!Local,currentLatitude!~

makeLocalAvailable ~!Local,value!~
