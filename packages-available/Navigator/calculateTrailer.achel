# Calculate how to get back on course using a metaphorical trailer bar to guide the steering. --calculateTrailer=instanceName ~ nav,waypoints,calculation
parameters instanceName

set Local,ready,1

if ~!AP,state,~!Local,instanceName!~,longitude-deg!~,==,,
    set Local,ready,0

if ~!AP,state,~!Local,instanceName!~,destination,longitude!~,==,,
    set Local,ready,0

if ~!Local,ready!~,==,1,
    # get origin
    setNested ["Navigator","deviance","origin",~!Navigator,origins,~!Local,instanceName!~!~]
    set Local,originX,~!Navigator,deviance,origin,longitude!~
    set Local,originY,~!Navigator,deviance,origin,latitude!~

    # get actual position
    set Local,pointX,~!AP,state,~!Local,instanceName!~,longitude-deg!~
    set Local,pointY,~!AP,state,~!Local,instanceName!~,latitude-deg!~
    setNested ["Navigator","deviance","position","longitude",~!Local,pointX!~]
    setNested ["Navigator","deviance","position","latitude",~!Local,pointY!~]

    # get destination
    setNested ["Navigator","deviance","destination",~!AP,state,~!Local,instanceName!~,destination!~]
    set Local,destinationX,~!AP,state,~!Local,instanceName!~,destination,longitude!~
    set Local,destinationY,~!AP,state,~!Local,instanceName!~,destination,latitude!~

    # Get orientation.
    set Local,orientationDegrees,~!AP,state,~!Local,instanceName!~,heading-deg!~
    degreesToRadians Local,orientation,~!Local,orientationDegrees!~

    # Get the linear calculations.
    linearPointStats Local,linearStats,~!Local,pointX!~,~!Local,pointY!~,~!Local,orientation!~,~!Local,originX!~,~!Local,originY!~,~!Local,destinationX!~,~!Local,destinationY!~
    set Local,deviance,~!Local,linearStats,distanceFromLine!~

    # Get speed.
    getAPVar ~!Local,instanceName!~,airspeed-kt,Local,speed

    # Calculate how long we have been close to the track.
    degreesToMeters Local,distanceInMeters,~!Local,linearStats,distanceFromLine!~
    absolute Local,absDistanceInMeters,~!Local,distanceInMeters!~
    microNow Local,now
    set Local,timeMultiplier,1
    set Local,proximityMultiplier,1
    # TODO Make the threshold configurable.
    basicMaths Local,thesholdBase,~!Local,speed!~,+,10
    basicMaths Local,distanceThreshold,~!Local,thesholdBase!~,*,5
    if ~!Local,absDistanceInMeters!~,>,~!Local,distanceThreshold!~,
        debug 2,outside close zone ~!Local,instanceName!~ ~!Local,absDistanceInMeters!~
        setAPVarWithContext ~!Local,instanceName!~,track,closeSince,~!Local,now!~
    else
        set Local,proximityMultiplier,2
        getAPVarWithContext ~!Local,instanceName!~,track,closeSince,Local,closeSince
        if ~!Local,closeSince!~,==,,
            setAPVarWithContext ~!Local,instanceName!~,track,closeSince,~!Local,now!~
            set Local,closeSince,~!Local,now!~
            debug 1,Set closeSince=~!Local,now!~

        basicMaths Local,closeTime,~!Local,now!~,-,~!Local,closeSince!~
        if ~!Local,closeTime!~,>,2,
            basicMaths Local,timeMultiplier,~!Local,closeTime!~,/,10

    # How long should the trailer bar be in meters?
    basicMaths Local,barLength,~!Local,speed!~,*,40
    basicMaths Local,barLength,~!Local,barLength!~,*,~!Local,timeMultiplier!~
    basicMaths Local,barLength,~!Local,barLength!~,*,~!Local,proximityMultiplier!~
    basicMaths Local,barLength,~!Local,barLength!~,+,10

    getAPVar ~!Local,instanceName!~,maxBarLength,Local,maxBarLength
    if ~!Local,barLength!~,>,~!Local,maxBarLength!~,
        set Local,barLength,~!Local,maxBarLength!~

    metersToDegrees Local,barLengthDegrees,~!Local,barLength!~

    # Calculate aiming point along the track in meters (so the differences don't get lost in rounding errors).
    basicMaths Local,cSquared,~!Local,barLength!~,^,2
    basicMaths Local,aSquared,~!Local,distanceInMeters!~,^,2
    basicMaths Local,bSquared,~!Local,cSquared!~,-,~!Local,aSquared!~
    basicMaths Local,bMeters,~!Local,bSquared!~,^,0.5
    metersToDegrees Local,b,~!Local,bMeters!~

    angleAndDistanceToCoord Local,bOffset,~!Local,linearStats,extra,lineAngle!~,~!Local,b!~
    basicMaths Local,aimX,~!Local,linearStats,extra,idealPosition,x!~,+,~!Local,bOffset,x!~
    basicMaths Local,aimY,~!Local,linearStats,extra,idealPosition,y!~,+,~!Local,bOffset,y!~

    # Calculate angular offset from the current point.
    2CoordsToAngle Local,rawAimAngle,~!Local,pointX!~,~!Local,pointY!~,~!Local,aimX!~,~!Local,aimY!~
    angleDiffRadians Local,aimAngle,~!Local,linearStats,extra,lineAngle!~,~!Local,rawAimAngle!~
    basicMaths Local,aimAngle,~!Local,aimAngle!~,*,-1

    angleDiffRadians Local,barAngle,~!Local,rawAimAngle!~,~!Local,orientation!~
    angleDiffRadians Local,headingOffset,~!Local,linearStats,extra,lineAngle!~,~!Local,orientation!~

    # Desensitise with time.
    basicMaths Local,timeDivider,~!Local,closeTime!~,/,100
    basicMaths Local,timeDivider,~!Local,timeDivider!~,+,1
    if ~!Local,timeDivider!~,>,10,
        set Local,timeDivider,10

    basicMaths Local,headingOffsetDesenitised,~!Local,headingOffset!~,/,~!Local,timeDivider!~



    # Figure out if heading and distance have the same polarity.
    set Local,distanceSide,-1
    if ~!Local,distanceInMeters!~,>,0,
        set Local,distanceSide,1

    set Local,headingSide,-1
    if ~!Local,headingOffset!~,>,0,
        set Local,headingSide,1

    set Local,hdDstPolarity,0
    if ~!Local,distanceSide!~,==,~!Local,headingSide!~,
        set Local,hdDstPolarity,1


    # New overshoot code.
    # If heading diff is greater than threshold.
    #   If the new distance is in the direction of closer to the line from the perspective of the old distance.
    #     Take absolute distance difference.
    #     Take absolute distance remaining.
    #     Take absolute remaining distance and divide by absolute distance difference.
    #     Divide by time difference.
    #
    #     If distance remaining is negative
    #       Invert.

    # Overshoot.
    # If heading diff is greater than threshold.
    set Local,shouldDoOvershoot,1
    set Local,overshootWhy,na
    getAPVar ~!Local,instanceName!~,overshootHeadingThresholdRadians,Local,overshootHeadingThresholdRadians
    angleDiffRadians Local,planeToLineAngle,~!Local,linearStats,extra,lineAngle!~,~!Local,orientation!~
    absolute Local,planeToLineAngleAbs,~!Local,planeToLineAngle!~
    if ~!Local,planeToLineAngleAbs!~,<,~!Local,overshootHeadingThresholdRadians!~,
        set Local,shouldDoOvershoot,0
        set Local,overshootWhy,angleThreshold: ~!Local,planeToLineAngleAbs!~ < ~!Local,overshootHeadingThresholdRadians!~

    # Test: If the new distance is in the direction of closer to the line from the perspective of the old distance.
    getAPVarWithContext ~!Local,instanceName!~,track,lastDistance,Local,lastDistance
    set Local,mute,0
    if ~!Local,lastDistance!~,==,,
        debug 1,No overshoot history. Setting it up now.
        set Local,lastDistance,~!Local,distanceInMeters!~
        setAPVarWithContext ~!Local,instanceName!~,track,lastDistance,~!Local,lastDistance!~
        setAPVarWithContext ~!Local,instanceName!~,track,lastNow,~!Local,now!~
        set Local,mute,1

    if ~!Local,distanceInMeters!~,==,~!Local,lastDistance!~,
        set Local,shouldDoOvershoot,0
        set Local,overshootWhy,noChange: ~!Local,distanceInMeters!~

    set Local,closer,0
    if ~!Local,lastDistance!~,<,0,
        if ~!Local,distanceInMeters!~,>,~!Local,lastDistance!~,
            if ~!Local,distanceInMeters!~,<,0,
                set Local,closer,1
    if ~!Local,lastDistance!~,>=,0,
        if ~!Local,distanceInMeters!~,<,~!Local,lastDistance!~,
            if ~!Local,distanceInMeters!~,>,0,
                set Local,closer,1

    # Don't apply overshoot if we aren't getting closer to the line.
    if ~!Local,closer!~,==,0,
        set Local,shouldDoOvershoot,0
        set Local,overshootWhy,notCloser: ~!Local,distanceInMeters!~ vs ~!Local,lastDistance!~

    # Overshoot should only apply when we are facing the same direction as travel.
    if ~!Local,hdDstPolarity!~,==,1,
        set Local,shouldDoOvershoot,0
        set Local,overshootWhy,heading and distance don't have the same polarity.

    # Detect bogus data.
    basicMaths Local,distanceDiff,~!Local,distanceInMeters!~,-,~!Local,lastDistance!~
    absolute Local,distanceDiffAbsolute,~!Local,distanceDiff!~
    getAPVarWithContext ~!Local,instanceName!~,track,lastDistanceDiffAbsolute,Local,lastDistanceDiffAbsolute
    if ~!Local,lastDistanceDiffAbsolute!~,==,,
        set Local,lastDistanceDiffAbsolute,~!Local,distanceDiffAbsolute!~
        setAPVarWithContext ~!Local,instanceName!~,track,lastDistanceDiffAbsolute,~!Local,lastDistanceDiffAbsolute!~
    else
        basicMaths Local,doubleLastDistanceDiffAbsolute,~!Local,lastDistanceDiffAbsolute!~,*,2
        if ~!Local,distanceDiffAbsolute!~,>,~!Local,doubleLastDistanceDiffAbsolute!~,
            debug 1,Bogus data detected (~!Local,distanceDiffAbsolute!~>~!Local,doubleLastDistanceDiffAbsolute!~). Skipping this iteration.
            set Local,shouldDoOvershoot,0
            set Local,overshootWhy,Bogus data (~!Local,distanceDiffAbsolute!~>~!Local,doubleLastDistanceDiffAbsolute!~).
            set Local,mute,1
            # The check will make less sense with each iteration. So this stops us from testing it on the next iteration.
            setAPVarWithContext ~!Local,instanceName!~,track,lastDistanceDiffAbsolute,
        setAPVarWithContext ~!Local,instanceName!~,track,lastDistanceDiffAbsolute,~!Local,distanceDiffAbsolute!~

    # Do the overshoot.
    if ~!Local,shouldDoOvershoot!~,==,1,
        basicMaths Local,stepsRemaining,~!Local,absDistanceInMeters!~,/,~!Local,distanceDiffAbsolute!~

        getAPVarWithContext ~!Local,instanceName!~,track,lastNow,Local,lastNow
        basicMaths Local,nowChange,~!Local,now!~,-,~!Local,lastNow!~
        basicMaths Local,metersPerSecond,~!Local,distanceDiffAbsolute!~,/,~!Local,nowChange!~
        basicMaths Local,overshootSeconds,~!Local,absDistanceInMeters!~,/,~!Local,metersPerSecond!~

        # basicMaths Local,overshootCounterAngle,~!Local,planeToLineAngle!~,/,~!Local,overshootSeconds!~
        # basicMaths Local,overshootCounterAngle,1,/,~!Local,overshootSeconds!~
        # basicMaths Local,overshootCounterAngle,~!Local,distanceDiffAbsolute!~,/,~!Local,overshootSeconds!~
        basicMaths Local,overshootCounterAngle,~!Local,metersPerSecond!~,/,~!Local,overshootSeconds!~

        # Change the shape.
        getAPVar ~!Local,instanceName!~,overshootMultiplier,Local,overshootMultiplier
        getAPVar ~!Local,instanceName!~,overshootOffset,Local,overshootOffset

        basicMaths Local,overshootCounterAngle,~!Local,overshootCounterAngle!~,*,~!Local,overshootMultiplier!~
        basicMaths Local,overshootCounterAngle,~!Local,overshootCounterAngle!~,+,~!Local,overshootOffset!~

        if ~!Local,overshootCounterAngle!~,<,0,
            set Local,overshootCounterAngle,0

        # Apply polarity based on the polarity of the distance.
        if ~!Local,distanceInMeters!~,>,0,
            basicMaths Local,overshootCounterAngle,~!Local,overshootCounterAngle!~,*,-1



        debug 1,Overshoot oA=~!Local,overshootCounterAngle!~  dC=~!Local,distanceDiff!~ tC=~!Local,nowChange!~ m/s=~!Local,metersPerSecond!~ oss=~!Local,overshootSeconds!~

        # Did we overshoot?
        set Local,before,0
        set Local,after,0
        if ~!Local,lastDistance!~,<,0,
            set Local,before,1
        if ~!Local,distanceInMeters!~,<,0,
            set Local,after,1

        if ~!Local,before!~,!=,~!Local,after!~,
            debug 1,---------------- Crossed the line. ----------------
            mark ~!Local,instanceName!~,Crossed the line.
    # TODO Change this back to else once it is known good again.
    if ~!Local,shouldDoOvershoot!~,!=,1,
        set Local,overshootCounterAngle,0

    setAPVarWithContext ~!Local,instanceName!~,track,lastDistance,~!Local,distanceInMeters!~
    setAPVarWithContext ~!Local,instanceName!~,track,lastNow,~!Local,now!~


    # Calculate FOMO from headingOffset.
    absolute Local,absoluteHeadingOffset,~!Local,headingOffset!~
    set Local,fomoValue,0
    if ~!Local,hdDstPolarity!~,==,1,
        if ~!Local,closer!~,!=,1,
            set Local,fomoValue,~!Local,absoluteHeadingOffset!~
            if ~!Local,distanceInMeters!~,<,0,
                basicMaths Local,fomoValue,~!Local,fomoValue!~,*,-1

    # Calculate nudge from distance if we aren't getting closer to the goal.
    set Local,nudge,0
    if ~!Local,closer!~,!=,1,
        set Local,nudge,~!Local,absDistanceInMeters!~
        if ~!Local,nudge!~,>,10,
            set Local,nudge,10

        if ~!Local,distanceInMeters!~,<,0,
            basicMaths Local,nudge,~!Local,nudge!~,*,-1

        basicMaths Local,nudge,~!Local,nudge!~,/,10


    # Assert that we are in the right trailerProfile.
    getAPVar ~!Local,instanceName!~,trailerProfileStart,Local,trailerProfileStart
    getAPVar ~!Local,instanceName!~,minSeconds,Local,minSeconds
    basicMaths Local,timeInCurrentProfile,~!Local,now!~,-,~!Local,trailerProfileStart!~

    if ~!Local,timeInCurrentProfile!~,>,~!Local,minSeconds!~,
        getAPVar ~!Local,instanceName!~,closeHeadingDiffRadians,Local,closeHeadingDiffRadians
        getAPVar ~!Local,instanceName!~,closeDistance,Local,closeDistance
        getAPVar ~!Local,instanceName!~,farHeadingDiffRadians,Local,farHeadingDiffRadians
        getAPVar ~!Local,instanceName!~,farDistance,Local,farDistance

        set Local,profileSelector,na
        if ~!Local,absoluteHeadingOffset!~,<,~!Local,closeHeadingDiffRadians!~,
            if ~!Local,absDistanceInMeters!~,<,~!Local,closeDistance!~,
                set Local,profileSelector,close

        if ~!Local,absoluteHeadingOffset!~,>,~!Local,farHeadingDiffRadians!~,
            if ~!Local,absDistanceInMeters!~,>,~!Local,farDistance!~,
                set Local,profileSelector,far

        set Local,trailerProfileToApply,na
        if ~!Local,profileSelector!~,==,close,
            getAPVar ~!Local,instanceName!~,trailerProfileClose,Local,trailerProfileToApply
            # debug 1,trailerProfile close because:  ho(~!Local,absoluteHeadingOffset!~) < ~!Local,closeHeadingDiffRadians!~  and  d(~!Local,absDistanceInMeters!~) < ~!Local,closeDistance!~
        if ~!Local,profileSelector!~,==,far,
            getAPVar ~!Local,instanceName!~,trailerProfileFar,Local,trailerProfileToApply
            # debug 1,trailerProfile far because:  ho(~!Local,absoluteHeadingOffset!~) > ~!Local,farHeadingDiffRadians!~  and  d(~!Local,absDistanceInMeters!~) > ~!Local,farDistance!~

        trailerProfile ~!Local,instanceName!~,~!Local,trailerProfileToApply!~


    # Apply weights.
    getAPVar ~!Local,instanceName!~,strengthTrailer,Local,strengthTrailer
    getAPVar ~!Local,instanceName!~,strengthHeading,Local,strengthHeading
    getAPVar ~!Local,instanceName!~,strengthBarAngle,Local,strengthBarAngle
    getAPVar ~!Local,instanceName!~,strengthOvershoot,Local,strengthOvershoot
    getAPVar ~!Local,instanceName!~,strengthFomo,Local,strengthFomo
    getAPVar ~!Local,instanceName!~,strengthNudge,Local,strengthNudge

    applyRangeAndMultiplier Local,adjustedTrailer,,~!Local,aimAngle!~,,~!Local,strengthTrailer!~
    applyRangeAndMultiplier Local,adjustedHeading,,~!Local,headingOffsetDesenitised!~,,~!Local,strengthHeading!~
    applyRangeAndMultiplier Local,adjustedBarAngle,,~!Local,barAngle!~,,~!Local,strengthBarAngle!~
    applyRangeAndMultiplier Local,adjustedOvershoot,,~!Local,overshootCounterAngle!~,,~!Local,strengthOvershoot!~
    applyRangeAndMultiplier Local,adjustedFomo,,~!Local,fomoValue!~,,~!Local,strengthFomo!~
    applyRangeAndMultiplier Local,adjustedNudge,,~!Local,nudge!~,,~!Local,strengthNudge!~

    basicMaths Local,finalAim,~!Local,adjustedTrailer!~,+,~!Local,adjustedHeading!~
    basicMaths Local,finalAim,~!Local,finalAim!~,+,~!Local,adjustedBarAngle!~
    basicMaths Local,finalAim,~!Local,finalAim!~,+,~!Local,adjustedOvershoot!~
    basicMaths Local,finalAim,~!Local,finalAim!~,+,~!Local,adjustedFomo!~
    basicMaths Local,finalAim,~!Local,finalAim!~,+,~!Local,adjustedNudge!~


    # Make it available to the AP.
    radiansToDegrees Local,aimAngle-deg,~!Local,finalAim!~
    if ~!Local,mute!~,!=,1,
        setAPVarWithContext ~!Local,instanceName!~,track,aimAngle,~!Local,finalAim!~
        setAPVarWithContext ~!Local,instanceName!~,track,aimAngle-deg,~!Local,aimAngle-deg!~
    else
        debug 1,Trailer muted for this iteration to allow everything to stablise after reset.

    # For use in the map.
    setAPVarWithContext ~!Local,instanceName!~,track,aimLon,~!Local,aimX!~
    setAPVarWithContext ~!Local,instanceName!~,track,aimLat,~!Local,aimY!~


    # debug 1,bOffset=~!Local,bOffset!~  lA=~!Local,linearStats,extra,lineAngle!~  b=~!Local,b!~(~!Local,bMeters!~)  bSquared=~!Local,bSquared!~  cSquared=~!Local,cSquared!~  aSquared=~!Local,aSquared!~  distanceInMeters=~!Local,distanceInMeters!~  barLength=~!Local,barLength!~(~!Local,barLengthDegrees!~)
    debug 2,d=~!Local,distanceInMeters!~(~!Local,linearStats,distanceFromLine!~) effort=~!Local,aimAngle-deg!~ s=~!Local,speed!~ barLength=~!Local,barLength!~ aim=~!Local,aimY!~,~!Local,aimX!~ closeTime=~!Local,closeTime!~=~!Local,now!~,-,~!Local,closeSince!~ multipliers=~!Local,timeMultiplier!~,~!Local,proximityMultiplier!~

    debug 2,effort=~!Local,aimAngle-deg!~  aimAngle=~!Local,aimAngleWithOvershoot!~=~!Local,aimAngle!~,-,~!Local,overshootCounterAngle!~  d=~!Local,distanceInMeters!~  angles: r=~!Local,rawAimAngle!~ h/o=~!Local,orientation!~

    # Generated with ./generateTrailerDebug.sh
    isolate
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,absDistanceInMeters,~!Local,absDistanceInMeters!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,absoluteHeadingOffset,~!Local,absoluteHeadingOffset!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,adjustedBarAngle,~!Local,adjustedBarAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,adjustedFomo,~!Local,adjustedFomo!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,adjustedHeading,~!Local,adjustedHeading!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,adjustedNudge,~!Local,adjustedNudge!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,adjustedOvershoot,~!Local,adjustedOvershoot!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,adjustedTrailer,~!Local,adjustedTrailer!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,after,~!Local,after!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,aimAngle,~!Local,aimAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,aimAngle-deg,~!Local,aimAngle-deg!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,aimX,~!Local,aimX!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,aimY,~!Local,aimY!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,aSquared,~!Local,aSquared!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,b,~!Local,b!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,barAngle,~!Local,barAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,barLength,~!Local,barLength!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,barLengthDegrees,~!Local,barLengthDegrees!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,before,~!Local,before!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,bMeters,~!Local,bMeters!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,bOffset-x,~!Local,bOffset,x!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,bOffset-y,~!Local,bOffset,y!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,branch,~!Local,branch!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,bSquared,~!Local,bSquared!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,closeDistance,~!Local,closeDistance!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,closeHeadingDiffRadians,~!Local,closeHeadingDiffRadians!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,closer,~!Local,closer!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,closeSince,~!Local,closeSince!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,closeTime,~!Local,closeTime!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,cSquared,~!Local,cSquared!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,destinationX,~!Local,destinationX!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,destinationY,~!Local,destinationY!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,deviance,~!Local,deviance!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,distanceDiff,~!Local,distanceDiff!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,distanceDiffAbsolute,~!Local,distanceDiffAbsolute!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,distanceInMeters,~!Local,distanceInMeters!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,distanceSide,~!Local,distanceSide!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,distanceThreshold,~!Local,distanceThreshold!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,farDistance,~!Local,farDistance!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,farHeadingDiffRadians,~!Local,farHeadingDiffRadians!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,finalAim,~!Local,finalAim!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,fomoValue,~!Local,fomoValue!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,hdDstPolarity,~!Local,hdDstPolarity!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,headingOffset,~!Local,headingOffset!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,headingOffsetDesenitised,~!Local,headingOffsetDesenitised!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,headingSide,~!Local,headingSide!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,lastDistance,~!Local,lastDistance!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,lastNow,~!Local,lastNow!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,maxBarLength,~!Local,maxBarLength!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,metersPerSecond,~!Local,metersPerSecond!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,minSeconds,~!Local,minSeconds!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,mute,~!Local,mute!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,now,~!Local,now!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,nowChange,~!Local,nowChange!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,nudge,~!Local,nudge!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,orientation,~!Local,orientation!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,orientationDegrees,~!Local,orientationDegrees!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,originX,~!Local,originX!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,originY,~!Local,originY!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,overshootCounterAngle,~!Local,overshootCounterAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,overshootHeadingThresholdRadians,~!Local,overshootHeadingThresholdRadians!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,overshootSeconds,~!Local,overshootSeconds!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,overshootWhy,~!Local,overshootWhy!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,planeToLineAngle,~!Local,planeToLineAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,planeToLineAngleAbs,~!Local,planeToLineAngleAbs!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,pointX,~!Local,pointX!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,pointY,~!Local,pointY!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,profileSelector,~!Local,profileSelector!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,proximityMultiplier,~!Local,proximityMultiplier!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,rawAimAngle,~!Local,rawAimAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,ready,~!Local,ready!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,shouldDoOvershoot,~!Local,shouldDoOvershoot!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,speed,~!Local,speed!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,stepsRemaining,~!Local,stepsRemaining!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,strengthBarAngle,~!Local,strengthBarAngle!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,strengthFomo,~!Local,strengthFomo!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,strengthHeading,~!Local,strengthHeading!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,strengthNudge,~!Local,strengthNudge!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,strengthOvershoot,~!Local,strengthOvershoot!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,strengthTrailer,~!Local,strengthTrailer!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,thesholdBase,~!Local,thesholdBase!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,timeDivider,~!Local,timeDivider!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,timeInCurrentProfile,~!Local,timeInCurrentProfile!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,timeMultiplier,~!Local,timeMultiplier!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,trailerProfileStart,~!Local,trailerProfileStart!~
        setAPVarWithContext ~!Local,instanceName!~,trailerDebug,trailerProfileToApply,~!Local,trailerProfileToApply!~
